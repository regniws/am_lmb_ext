
#Область СобытияФормы

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	НастроитьФорму();
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ПрочитатьДанные(Команда)
	
	Если НЕ КонтрольПараметров() Тогда
		Возврат;
	КонецЕсли;
	
	ЗФ_ОбщегоНазначениКлиентСервер.ПрочитатьДанные(
		ПараметрыПоПрограмме());
	
	НастроитьВозможностьЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьДоФайлаТЛСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Фильтр = НСтр("ru = 'dbf'; en = 'dbf'") + "(*.dbf*)|*.dbf*";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл для загрузки";
	ДиалогОткрытияФайла.ПроверятьСуществованиеФайла = Истина;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено
		И ВыбранныеФайлы.Количество()>0 Тогда
		
		ПутьДоТЛС = ВыбранныеФайлы[0];
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

#КонецОбласти

#Область ПрочиеОбработчики

&НаСервере
Процедура НастроитьФорму()
	Элементы.ГруппаНастройкиАдаптация.ТекущаяСтраница = Элементы["Настройка"+Программа]
КонецПроцедуры

&НаКлиенте
Функция КонтрольПараметров()
	Если НЕ ЗНачениеЗаполнено(Программа) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru='Не выбрана программа для загрузки'"),,"Программа");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ЗФ_ОбщегоНазначениКлиентСервер.КонтрольПараметровЗагрузки(
		ПараметрыПоПрограмме());
КонецФункции

&НаКлиенте
Функция ПараметрыПоПрограмме()
	
	Возврат ЗФ_ОбщегоНазначениКлиентСервер.ПараметрыПоПрограмме(ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура НастроитьВозможностьЗагрузки()
	
	ПроконтролироватьТаблицы();
	
	ЕстьОшибки = Клиенты.НайтиСтроки(Новый Структура("ОшибкаОбработки", Истина)).Количество() > 0;
	ЕстьОшибки = ЕстьОшибки ИЛИ Залоги.НайтиСтроки(Новый Структура("Ошибка", Истина)).Количество() > 0;
	ЕстьОшибки = ЕстьОшибки ИЛИ Вещи.НайтиСтроки(Новый Структура("Ошибка", Истина)).Количество() > 0;
	
	Если ЕстьОшибки Тогда
		Элементы.ФормаВыполнитьЗагрузку.Заголовок = НСтр("ru='В данных есть ошибка. Загрузка невозможна'");
	Иначе
		Элементы.ФормаВыполнитьЗагрузку.Доступность = Истина;
		Элементы.ФормаВыполнитьЗагрузку.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаВыполнитьЗагрузку.Заголовок = НСтр("ru='Выполнить загрузку'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроконтролироватьТаблицы()

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ВыполнитьЗагрузкуНаСервере(),
		Новый ОписаниеОповещения("ПослеЗагрузки", ЭтотОбъект),
		ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузки(Результат, ПараметрыМетода) Экспорт
	
	Элементы.ГруппаРезультат.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат;
	
	Если Результат = Неопределено Тогда
		РезультатЗагрузки.УстановитьТекст(НСтр("ru='Загрузка не завершилась. Причины не понятны. Обратитесь в поддержку.'"));
		Возврат;
	КонецЕсли;
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.Партии"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.ФизическиеЛица"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.БилетыКвитанции"));
	ОповеститьОбИзменении(Тип("ДокументСсылка._3_3_1_ОтправитьПартии"));
	
	ДанныеРезультата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ЗначениеЗаполнено(ДанныеРезультата) И ДанныеРезультата.Результат Тогда
		РезультатЗагрузки.УстановитьТекст(НСтр("ru='Загрузка выполнена успешно. Обработайте адреса клиентов и сформируйте рейсы 3.8'"));
		Элементы.ГруппаНавигации.Видимость = Истина;
	Иначе	
		Если ТипЗнч(ДанныеРезультата) = Тип("Структура") И ДанныеРезультата.Свойство("Описание") Тогда
			РезультатЗагрузки.УстановитьТекст(ДанныеРезультата.Описание);
		Иначе
			РезультатЗагрузки.УстановитьТекст(НСтр("ru='Загрузка не завершилась. Причины не понятны. Обратитесь в поддержку.'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВыполнитьЗагрузкуНаСервере()
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор),
		"ЗФ_ОбщегоНазначения.ЗагрузитьДанные",
		Клиенты.Выгрузить(),
		Залоги.Выгрузить(),
		Вещи.Выгрузить(), 
		НЕ ПередаватьПерсональныеДанные);
	
КонецФункции

&НаКлиенте
Процедура Декорация1Нажатие(Элемент)
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	ОткрытьФорму("Документ._3_8_СкупкаЛомбард.ФормаСписка");
КонецПроцедуры

#КонецОбласти


#Область РасширяемыеФункцииИПроцедуры

#КонецОбласти
